plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    implementation project(':spark-common')
    implementation 'net.kyori:adventure-text-serializer-legacy:4.13.1'
    compileOnly 'com.github.PowerNukkitX:PowerNukkitX:master-SNAPSHOT'
    compileOnly 'com.google.guava:guava:33.0.0-jre'
    compileOnly 'org.projectlombok:lombok:1.18.30'
}

tasks.withType(JavaCompile) {
    options.release = 17
}

repositories {
    maven { url 'https://repo.opencollab.dev/main/' }
    maven { url 'https://repo.maven.apache.org/maven2/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.opencollab.dev/maven-releases/' }
    maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-pnx.jar"

    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}
